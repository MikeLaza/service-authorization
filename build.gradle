plugins {
    id 'net.researchgate.release' version '2.8.0'
    id "com.jfrog.bintray" version "1.8.4"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id 'java'
    id "org.ajoberstar.grgit" version "3.1.1"
}

description = 'EPAM Report portal. SSO Authorization Service'

apply from: 'build-quality.gradle'
apply from: 'release.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-docker.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-info.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-commons.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext {
    releaseMode = project.hasProperty("releaseMode")
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url "https://jitpack.io" }
}

dependencyManagement {
    imports {
        mavenBom "com.github.reportportal:commons-bom:f060f9c"
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-dao:5.0.0-RC-2'
        compile 'com.epam.reportportal:commons-rules:5.0.0-BETA-6'
        compile 'com.epam.reportportal:commons-model:5.0.0-RC-1'
    } else {
        compile 'com.github.reportportal:commons-dao:523ba3f'
        compile 'com.github.reportportal:commons-rules:5.0.0-BETA-6'
        compile 'com.github.reportportal:commons-model:9840721'
    }

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-security'

    compile 'org.springframework.security:spring-security-oauth2-client'
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE'
    compile 'org.springframework.security:spring-security-jwt:1.0.11.RELEASE'
    compile 'org.springframework.security:spring-security-ldap'
    compile 'org.springframework.security.extensions:spring-security-saml2-core:2.0.0.M26'

    compile 'org.apache.tika:tika-core'
    compile 'org.apache.tika:tika-parsers'

    compile('io.springfox:springfox-swagger2')
}

wrapper {
    gradleVersion = '5.4.1'
}

jar {
    if (!releaseMode) {
        getArchiveFileName().set("${archivesBaseName}.${archiveExtension}")
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    manifest {
        attributes 'Implementation-Title': 'ReportPortal Auth Server Commons',
                'Implementation-Version': archiveVersion,
                'Main-Class': 'com.epam.reportportal.auth.AuthServerApplication',
                'Class-Path': configurations.runtime.files.collect { "lib/$it.name" }.join(' ')
    }
}

