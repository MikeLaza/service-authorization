plugins {
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id "org.owasp.dependencycheck" version "5.2.1"
}

apply from: 'project-properties.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-docker.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-info.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-commons.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-quality.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/release-service.gradle'

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url "https://jitpack.io" }
}

dependencyManagement {
    imports {
        mavenBom "com.github.reportportal:commons-bom:f060f9c"
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-dao:5.0.0-RC-2'
        compile 'com.epam.reportportal:commons-rules:5.0.0-BETA-6'
        compile 'com.epam.reportportal:commons-model:5.0.0-RC-1'
    } else {
        compile 'com.github.reportportal:commons-dao:3e9c759'
        compile 'com.github.reportportal:commons-rules:5.0.0-BETA-6'
        compile 'com.github.reportportal:commons-model:68fb20b'
    }

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-security'

    compile 'org.springframework.security:spring-security-oauth2-client'
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE'
    compile 'org.springframework.security:spring-security-jwt:1.0.11.RELEASE'
    compile 'org.springframework.security:spring-security-ldap'
    compile 'org.springframework.security.extensions:spring-security-saml2-core:2.0.0.M26'

    compile 'org.apache.tika:tika-core'
    compile 'org.apache.tika:tika-parsers'

    compile('io.springfox:springfox-swagger2')
}

processResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

jar {
    if (!releaseMode) {
        getArchiveFileName().set("${getArchiveBaseName().get()}.${getArchiveExtension().get()}")
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

processResources.dependsOn gitInfo
jar.dependsOn gitInfo
prepareDockerfile.dependsOn build
buildDocker.dependsOn prepareDockerfile
addDockerfileToGit.dependsOn releaseDockerfile
updateVersion.dependsOn removeDockerfileFromGit
beforeReleaseBuild.dependsOn addDockerfileToGit
afterReleaseBuild.dependsOn bintrayUpload

